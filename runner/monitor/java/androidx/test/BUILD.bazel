load("//build_extensions:axt_android_aar.bzl", "axt_android_aar")
load("//build_extensions:maven_artifact.bzl", "maven_artifact")
load("@io_bazel_rules_kotlin//kotlin:android.bzl", "kt_android_library")
load("//build_extensions:axt_versions.bzl", "MONITOR_ARTIFACT")

# Description: Build rules for building androidx.test from source
licenses(["notice"])

package(
    default_visibility = ["//visibility:public"],
)

kt_android_library(
    name = "monitor",
    srcs = glob(
        [
            "**/*.java",
            "**/*.kt",
        ],
        exclude = [
            "internal/runner/runtime/ExposedInstrumentationApi.java",
            "internal/runner/hidden/ExposedInstrumentationApi.java",
            "internal/platform/app/ActivityInvoker$$CC.java",
        ],
    ),
    tags = [
        "maven_coordinates=%s" % MONITOR_ARTIFACT,
    ],
    deps = [
        ":compiletime_hidden_apis",
        ":errorprone_neverlink",
        ":runtime_hidden_apis",
        "//:androidx_annotation",
        "//:androidx_tracing",
        "//annotation",
    ],
)

# avoid the runtime dependency on errorprone
java_library(
    name = "errorprone_neverlink",
    neverlink = 1,
    exports = [
        "@maven//:com_google_errorprone_error_prone_annotations",
    ],
)

android_library(
    name = "runtime_hidden_apis",
    srcs = [
        "internal/runner/runtime/ExposedInstrumentationApi.java",
    ],
    tags = [
        "maven_coordinates=%s" % MONITOR_ARTIFACT,
    ],
    deps = [
        "//:androidx_annotation",
    ],
)

android_library(
    name = "compiletime_hidden_apis",
    srcs = [
        "internal/runner/hidden/ExposedInstrumentationApi.java",
    ],
    neverlink = 1,
    deps = [
        "//:androidx_annotation",
    ],
)

# group of targets to use to produce release binary + docs
android_library(
    name = "monitor_release_lib",
    srcs = [
        # only needed for external release backwards compatibility
        "internal/platform/app/ActivityInvoker$$CC.java",
    ],
    custom_package = "androidx.test.monitor",
    manifest = "AndroidManifest.xml",
    proguard_specs = [":proguard_library.cfg"],
    tags = [
        "maven_coordinates=%s" % MONITOR_ARTIFACT,
    ],
    deps = [
        ":monitor",
        "//:androidx_annotation",
    ],
)

# Generate rules for the release artifacts.
axt_android_aar(
    name = "monitor_release",
    included_dep = ":monitor_release_lib",
)

maven_artifact(
    name = "monitor_maven_artifact",
    last_updated = "20170622000000",
    target = ":monitor_release",
)
